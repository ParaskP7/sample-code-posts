ext.versions = [

        // Plugins
        gradleVersionsPluginVersion: '0.15.0',
        androidPluginVersion       : '3.0.0-beta6',
        realmPluginVersion         : '3.5.0',

        // Quality Plugins
        checkstylePluginVersion    : '8.0',
        pmdPluginVersion           : '5.8.1',
        findbugsPluginVersion      : '3.0.1',
        jacocoPluginVersion        : '0.7.9',

        // Build
        minSdkVersion              : 15,
        targetSdkVersion           : 26,
        compileSdkVersion          : 26,
        buildToolsVersion          : '26.0.1',

        // Android
        supportLibrariesVersion    : '26.0.1',

        // Important
        mosbyMvpVersion            : '3.0.4',
        dagger2Version             : '2.11',
        butterKnifeVersion         : '8.8.0',
        retrofit2Version           : '2.3.0',

        // ReactiveX
        rxAndroid2Version          : '2.0.1',
        rxJava2Version             : '2.1.2',

        // Image
        picassoVersion             : '2.5.2',
        circleImageViewVersion     : '2.1.0',

        // Other
        timberVersion              : '4.5.1',
        findbugsVersion            : '2.0.1',

        // Debug
        rxLintVersion              : '1.4',

        // Test
        junitVersion               : '4.12',
        assertJJava8Version        : '3.8.0',
        assertJJava7Version        : '3.8.0',
        mockitoVersion             : '2.8.47',
        rxJavaVersion              : '1.3.0',
        robolectricVersion         : '3.4.2',
        robolectricRCVersion       : "3.4-rc2",
        espressoVersion            : '3.0.0',
        androidTestRunnerVersion   : '1.0.0',

]

ext {
    /**
     * Builds an Android version code from the version of the project.
     * This is designed to handle the -SNAPSHOT and -RC format.
     *
     * I.e. during development the version ends with -SNAPSHOT. As the code stabilizes and release nears
     * one or many Release Candidates are tagged. These all end with "-RC1", "-RC2" etc.
     * And the final release is without any suffix.
     * @return
     */
    buildVersionCode = {
        //The rules is as follows:
        //-SNAPSHOT counts as 0
        //-RC* counts as the RC number, i.e. 1 to 98
        //final release counts as 99.
        //Thus you can only have 98 Release Candidates, which ought to be enough for everyone

        def candidate = "99"
        def (major, minor, patch) = version.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]", "")
        } else {
            def rc
            (patch, rc) = patch.tokenize("rc")
            if (rc) {
                candidate = rc
            }
        }

        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect {
            it.toInteger()
        }

        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate;
    }

}
