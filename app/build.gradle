buildscript {

    repositories {

        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com'
        }

    }

    dependencies {

        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradleVersionsPluginVersion"
        classpath "com.android.tools.build:gradle:$versions.androidPluginVersion"
        classpath "io.realm:realm-gradle-plugin:$versions.realmPluginVersion"

    }

}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.android.application'
apply plugin: 'realm-android'

apply from: 'config/gradle/quality.gradle'
apply from: 'config/gradle/jacoco.gradle'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

android {

    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion versions.buildToolsVersion
    defaultConfig {
        applicationId group // Comes from: gradle.properties
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        vectorDrawables.useSupportLibrary = true // For vector image support. Could be only used with Gradle plugin 2.0+.
        versionCode buildVersionCode() // Comes from: versioning.gradle
        versionName version // Comes from: gradle.properties
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
        }
    }

    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            systemProperty 'robolectric.logging.enabled', 'true'
            systemProperty 'robolectric.logging', 'stdout'
            // Print test details in the terminal (or CI).
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat 'full'
            }
        }
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {

    // Android Support
    implementation "com.android.support:support-annotations:$versions.supportLibrariesVersion"
    implementation "com.android.support:appcompat-v7:$versions.supportLibrariesVersion"
    implementation "com.android.support:recyclerview-v7:$versions.supportLibrariesVersion"
    implementation "com.android.support:cardview-v7:$versions.supportLibrariesVersion"
    implementation "com.android.support:design:$versions.supportLibrariesVersion"
    // MVP
    implementation "com.hannesdorfmann.mosby3:mvp:$versions.mosbyMvpVersion"
    implementation "com.hannesdorfmann.mosby3:mvp-lce:$versions.mosbyMvpVersion"
    implementation "com.hannesdorfmann.mosby3:mvp-nullobject-presenter:$versions.mosbyMvpVersion"
    // Dependency Injection
    implementation "com.google.dagger:dagger:$versions.dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger2Version"
    compileOnly "javax.annotation:jsr250-api:1.0"
    // Binding
    implementation "com.jakewharton:butterknife:$versions.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterKnifeVersion"
    // REST
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit2Version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit2Version"
    // ReactiveX
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid2Version"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava2Version"
    // Image
    implementation "com.squareup.picasso:picasso:$versions.picassoVersion"
    implementation "de.hdodenhof:circleimageview:$versions.circleImageViewVersion"
    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timberVersion"
    // @Nonnull/@Nullable Annotations
    implementation "com.google.code.findbugs:annotations:$versions.findbugsVersion"
    // Debug - Rx lint is a single lint check that checks if an RxJava subscriber is handling the onError() callback.
    debugImplementation "nl.littlerobots.rxlint:rxlint:$versions.rxLintVersion"
    // Unit Test
    testImplementation "junit:junit:$versions.junitVersion"
    testImplementation "org.assertj:assertj-core:$versions.assertJJava8Version"
    // Mockito
    testImplementation "org.mockito:mockito-core:$versions.mockitoVersion"
    testImplementation "io.reactivex:rxjava:$versions.rxJavaVersion" // Realm issue: java.lang.NoClassDefFoundError: rx.Observable
    // Robolectric Test
    testImplementation "org.robolectric:robolectric:$versions.robolectricVersion"
    testImplementation "org.robolectric:shadows-support-v4:$versions.robolectricRCVersion"
    testImplementation "org.khronos:opengl-api:gl1.1-android-2.1_r1" // See https://github.com/robolectric/robolectric/issues/1932
    // Instrumentation Test
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$versions.espressoVersion")
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$versions.espressoVersion") {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$versions.espressoVersion"
    androidTestImplementation "com.android.support.test:runner:$versions.androidTestRunnerVersion"
    androidTestImplementation "com.android.support:support-annotations:$versions.supportLibrariesVersion" // Fixes conflict issue.
    androidTestImplementation "org.assertj:assertj-core:$versions.assertJJava7Version" // To avoid dex error java 7 library is used.

}
